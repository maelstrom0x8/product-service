buildscript {
  dependencies {
    classpath 'org.postgresql:postgresql:42.6.0'
    classpath 'org.flywaydb:flyway-database-postgresql:10.4.1'
  }
}

plugins {
  id 'java'
  id 'org.springframework.boot' version '3.2.2'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'org.graalvm.buildtools.native' version '0.9.28'
  id "org.flywaydb.flyway" version '10.0.0'
  id 'com.diffplug.spotless' version '6.23.3'
  id 'com.palantir.git-version' version '0.15.0'
  id "com.google.osdetector" version "1.7.3"
}

group = 'com.marktplatz'
version = gitVersion()

java {
  sourceCompatibility = '17'
}

repositories {
  mavenCentral()
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-data-redis'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
  runtimeOnly 'org.postgresql:postgresql'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.boot:spring-boot-testcontainers'
  testImplementation 'org.testcontainers:junit-jupiter'
  testImplementation 'org.testcontainers:postgresql'
}

test {
  useJUnitPlatform()
  testLogging {
    events = ["PASSED", "FAILED", "SKIPPED"]
    showStandardStreams = true
    exceptionFormat = "full"
  }
}


spotless {
  java {
    target 'src/**/*.java'
    googleJavaFormat('1.17.0')
    licenseHeaderFile "${rootDir}/gradle/spotless.java.license.txt"
    ratchetFrom 'origin/master'
    trimTrailingWhitespace()
    endWithNewline()
    targetExclude '**/Tmp*.java'
  }
  groovyGradle {
    target '**/*.gradle'
    greclipse()
    indentWithSpaces(2)
    trimTrailingWhitespace()
    endWithNewline()
  }
}

tasks.named("bootBuildImage") {
  builder = "dashaun/builder:tiny"
  environment = ["BP_NATIVE_IMAGE": "true"]
  imageName = "maelstrom0x8/${rootProject.getName()}:${project.version}"
}
